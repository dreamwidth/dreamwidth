#!/usr/bin/perl
#
# bin/update-tor-exits
#
# This updates the list of Tor exits that can reach the local site.  You must
# configure $LJ::USE_TOR_CONFIGS and $LJ::EXTERNAL_IP for this script to work.
# You should install this script as a cron job, and run it every day or two.
#
# Authors:
#      Mark Smith <mark@dreamwidth.org>
#
# Copyright (c) 2010 by Dreamwidth Studios, LLC.
#
# This program is free software; you may redistribute it and/or modify it under
# the same terms as Perl itself.  For a copy of the license, please reference
# 'perldoc perlartistic' or 'perldoc perlgpl'.
#

use strict;
use lib "$ENV{LJHOME}/cgi-bin";
BEGIN {
    require 'ljlib.pl';
}

die "\$LJ::USE_TOR_CONFIGS is disabled.\n" unless $LJ::USE_TOR_CONFIGS;
die "\$LJ::EXTERNAL_IP is not set.\n" unless $LJ::EXTERNAL_IP;

my $ua = LJ::get_useragent( role => 'tor', timeout => 30 ) or die "No useragent.\n";
$ua->agent( "$LJ::SITENAME (Tor exit downloader; $LJ::ADMIN_EMAIL)" );

my $res = $ua->get( "https://check.torproject.org/cgi-bin/TorBulkExitList.py?ip=" . $LJ::EXTERNAL_IP );
my $content = $res && $res->is_success ? $res->content : undef;

die "Failed to fetch exit list from Tor.\n" unless $content;

my $dbh = LJ::get_db_writer() or die "Failed to get dbh.\n";
$dbh->do( 'DELETE FROM tor_proxy_exits' );

my $ct = 0;
while ( $content =~ /^(\d+\.\d+\.\d+\.\d+)$/mg ) {
    $ct++;

    $dbh->do( 'REPLACE INTO tor_proxy_exits (addr) VALUES (?)', undef, $1 );
    die "Failed on $1: " . $dbh->errstr . "\n" if $dbh->err;
}

print "Found $ct Tor exits.\n";
