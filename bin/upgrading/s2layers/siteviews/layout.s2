layerinfo "type" = "layout";
layerinfo "name" = "Sitescheme views";
layerinfo redist_uniq = "siteviews/layout";
layerinfo is_internal = "1";

class Siteviews {
    function builtin need_res(string res);
    function builtin start_capture();
    function builtin end_capture() : string;

    function builtin set_content(string area, string text);
}

property builtin Siteviews SITEVIEWS {
    noui = 1;
    doc_flags = "[sys]";
    des = "Instance of the Siteviews class";
}

function Page::print() {
    if ( $*SITEVIEWS ) {
        $*SITEVIEWS->start_capture();
        $this->print_head();
        $this->print_default_stylesheet();
        $this->print_theme_stylesheet();
        $*SITEVIEWS->set_content("head",$*SITEVIEWS->end_capture());
        $this->print_title();

        $this->print_body();
    } else {
       print safe "<b>This style is not intended for direct use.</b>";
    }
}

function Page::print_title() {
    # Do not actually *print* anything here, or in overridden instances, just call set_content with windowtitle or title as appropriate.
    $*SITEVIEWS->set_content("windowtitle",$this->view_title());
}

function Page::print_default_stylesheet() {
    # Do not actually *print* any stylesheets here, but you can $*SITEVIEWS->need_res(...); here to pull in anything.
}

function Page::print_theme_stylesheet() {}

# ItemRange

function ItemRange::print(string{} opts) {
    if ($.all_subitems_displayed) { return; }

    var string anchor = $opts{"anchor"} ? "#$opts{"anchor"}" : "";
    var string class = $opts{"class"} ? $opts{"class"} : "action-box";
    var string all_url = $opts{"all_url"} ? $opts{"all_url"} : "";

    """<div class="$class">""";
    print """<p style="font-weight: bolder; margin: 0 0 .5em 0;">""" + lang_page_of_pages($.current, $.total) + "</p>";
    var string url_prev = $this->url_of($.current - 1);
    if ($.current != 1) {
        print " <b><a href='$url_prev$anchor'>$*comment_page_prev</a></b> ";
    } else {
        print " <b>$*comment_page_prev</b> ";
    }
    """<span style="text-align: center">""";
    foreach var int i (1..$.total) {
        if ($i == $.current) { "<b>[$i]</b> "; }
        else {
            var string url_of = $this->url_of($i);
            "<a href='$url_of$anchor'><b>[$i]</b></a> ";
        }
    }
    """</span>""";
    var string url_next = $this->url_of($.current + 1);
    if ($.current != $.total) {
         print " <b><a href='$url_next$anchor'>$*comment_page_next</a></b> ";
    } else {
         print " <b>$*comment_page_next</b> ";
    }

    if ( $all_url != "" ) {
        """<p><a href="$all_url">View All</a></p>""";
    }

    "</div>";
}

# IconsPage

set text_icons_keyword_sep = ", ";
set text_icons_inactive = "[Inactive]";

function IconsPage::print_title() {
    # Do not actually *print* anything here, or in overridden instances, just call set_content with windowtitle or title as appropriate. 
    $*SITEVIEWS->set_content("title",$this->view_title());
}

function IconsPage::print_default_stylesheet() {
    # Do not actually *print* any stylesheets here, but you can $*SITEVIEWS->need_res(...); here to pull in anything.
    $*SITEVIEWS->need_res( "stc/allpics.css" );
}

function IconsPage::print_body() {
    var int piccount = 0;
    if ( size($.icons) == 0 ) {
        """<h1>$*text_icons_page_empty_header</h1><p>""";
        if ( $.can_manage ) {
            "You don't have any icons uploaded. <a href=\"" + $.journal->icon_manage_url() + "\">Upload an icon here</a>.";
        } else {
            """$.journal hasn't uploaded any icons. You can <a href="$*SITEROOT/manage/subscriptions/user?journal=$.journal.username">be notified</a> when $.journal uploads an icon, if you want.""";
        }
    } else {    
        """<h1>$*text_icons_page_header</h1><p>""";
        print "These are the icons for ";
        print $.journal;
        print ". ";
        if ( $.can_manage ) {
            "Would you like to <a href=\"" + $.journal->icon_manage_url() + "\">upload a new icon or edit your icon keywords, comments, and descriptions</a>?";
        } else {
            """We can <a href="$*SITEROOT/manage/tracking/user?journal=$.journal.username">notify</a> you when this account uploads a new one.""";
        }
        """</p>""";

        $.pages->print({ "anchor" => "", "all_url" => "$.journal.userpic_listing_url?view=all" });

        """<p>""";
        var int sort_ct = 0;
        foreach var string k ($.sort_keyseq) {
            var string text = lang_icon_sortorder_title($k);
            if ( $k == $.sortorder ) {
                print safe """$text""";
            } else {
                print safe """<a href='$.sort_urls{$k}'>$text</a>""";
            }
            if ( (++$sort_ct) < size $.sort_keyseq) { print $*text_default_separator; }
        }
        
        """<div class="icon-container">""";
        foreach var Icon i ($.icons) {
            $piccount++;

            if ($piccount % 2 == 1) {
                """<div class="icon-row">""";
            }

            $i->print();

            if ($piccount % 2 == 1) {
                
            } else {
                """</div>""";
            }
        }
        if ($piccount) {
            if ($piccount % 2 == 1) {
                """</div>""";
            }
            
        } else {
        }
        """</div>""";

        $.pages->print({ "anchor" => "", "all_url" => "$.journal.userpic_listing_url?view=all" });
        """<div class="clear-floats"></div>""";
    }
}

