#!/usr/bin/perl
# This code was forked from the LiveJournal project owned and operated
# by Live Journal, Inc. The code has been modified and expanded by 
# Dreamwidth Studios, LLC. These files were originally licensed under
# the terms of the license supplied by Live Journal, Inc, which can
# currently be found at:
#
# http://code.livejournal.org/trac/livejournal/browser/trunk/LICENSE-LiveJournal.txt
#
# In accordance with the original license, this code and all its
# modifications are provided under the GNU General Public License. 
# A copy of that license can be found in the LICENSE file included as
# part of this distribution.

use strict;
BEGIN {
    require "$ENV{LJHOME}/cgi-bin/ljlib.pl";
}

use LJ::Worker::TheSchwartz;
use TheSchwartz::Worker::SendEmail;
use Time::HiRes ();

# Intercept the email from the worker class so we can generate some statistics
# on it.
my $COUNT = 0;
my $send_job = \&TheSchwartz::Worker::SendEmail::_send_job_on_connection;
*TheSchwartz::Worker::SendEmail::_send_job_on_connection = sub {
    my ( $class, $smtp, $job ) = @_;
    my $fjob = FakeJob->new( $job );

    $COUNT++;
    my $rv = $send_job->( $class, $smtp, $fjob );
    DW::Stats::increment( 'dw.email.sent', 1, [ "status:$fjob->{invoked_last}", "via:direct" ] );

    return $rv;
};

TheSchwartz::Worker::SendEmail->set_HELO($LJ::DOMAIN);
TheSchwartz::Worker::SendEmail->set_keep_exit_status(sub { $LJ::KEEP_EXITSTATUS{'SendEmail'} || 0 });
TheSchwartz::Worker::SendEmail->set_on_5xx(sub {
    my ($rcpt, $job, $details) = @_;
    LJ::errobj('DieString', message => "send_email to $rcpt failed: $details")->log;
});

my $sclient = LJ::theschwartz();
$sclient->set_verbose(sub {
    my ($msg, $job) = @_;
    $msg =~ s/\s+$//;
    warn "$msg\n";
    return unless $LJ::DEBUG{'ts-sendemail'};
    return if $msg =~ /found no jobs$/;

    my $handle = $job->handle->as_string;
    my $now = sprintf("%0.05f", Time::HiRes::time());
    LJ::errobj('DieString', message => "ts_info: $now: [$handle] $msg")->log;
});

schwartz_decl('TheSchwartz::Worker::SendEmail');
schwartz_on_idle(sub {
    $0 = "send-email [idle, $COUNT]";
});
schwartz_work();


################################################################################
## FakeJob
##

package FakeJob;

sub new {
    my ( $class, $job ) = @_;
    my $self = { job => $job };
    return bless $self, $class;
}

sub AUTOLOAD {
    my $self = shift;
    my $method = $FakeJob::AUTOLOAD =~ /^FakeJob::(.+)$/ ? $1 : undef;
    $self->{invoked_last} = $method;
    return $self->{job}->$method( @_ );
}
