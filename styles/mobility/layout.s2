layerinfo "type" = "layout";
layerinfo "name" = "Mobility";
layerinfo author_name = "branchandroot";
layerinfo redist_uniq = "mobility/layout";
layerinfo lang = "en";

set layout_authors = [ { "name" => "branchandroot", "type" => "user" } ];

##===============================
## Presentation
##===============================

propgroup presentation_child {
    property string content_width {
        des = "Set the maximum width of the page content. Ensure that the value is given in em or px.";
        note = "This restricts the page width so it can be viewed on small screens without scrolling.";
        example = "50em";
    }
    property bool control_strip_reduced {
        des = "Reduce the navigation strip to icon and common links only";
    }
    property use entry_interaction_links;
    property use comment_interaction_links;
}

set layout_type = "one-column-split";
set control_strip_reduced = true;
set content_width = "55em";
set sidebar_width = "10em";
set sidebar_width_doubled = "20em";
set entry_userpic_style = "small";
set comment_userpic_style = "smaller";
set entry_interaction_links = "icon";
set entry_management_links = "icon";
set comment_interaction_links = "icon";
set comment_management_links = "icon";

##===============================
## Colors
##===============================

propgroup colors_child {
    property use color_header_link;
    property use color_header_link_active;
    property use color_header_link_hover;
    property use color_header_link_visited;
    property Color color_header_footer_border { des = "Page header and page footer border color"; }
    property Color color_module_highlight { des = " Calendar module background color on days with entries"; }
}

##===============================
## Modules
##===============================

# Explicitly define what sections the layout has available
set module_layout_sections = "none|(none)|one|First Module Section|two|Second Module Section";

set module_navlinks_section = "one";
set module_cuttagcontrols_section = "one";

set module_userprofile_section = "two";
set module_calendar_section = "two";
set module_links_section = "two";
set module_syndicate_section = "two";
set module_tags_section = "two";
set module_pagesummary_section = "two";
set module_active_section = "two";
set module_customtext_section = "two";
set module_credit_section = "two";
set module_search_section = "two";

set module_syndicate_order = 13;
set module_customtext_order = 5;
set module_search_order = 9;
set module_time_order = 18;
set module_poweredby_order = 19;

##===============================
## Functions
##===============================

function Page::print_header() {
    print """<h2>""" + $this.journal.username + """</h2>""" + """<h2>""" + $*text_default_separator + """</h2>""";
    $this->print_title();
    $this->print_module_jump_link();
}

function print_stylesheet () {
    var string page_background = generate_background_css ($*image_background_page_url, $*image_background_page_repeat, $*image_background_page_position, $*color_page_background);
    var string header_background = generate_background_css ($*image_background_header_url, $*image_background_header_repeat, $*image_background_header_position, $*color_header_background);
    if ($*image_background_header_height > 0) {
        $header_background = """
            $header_background
            height: """ + $*image_background_header_height + """px;""";
    }

    var string footer_background = generate_background_css( "", "", "", $*color_footer_background );
    var string footer_link_colors = generate_color_css( $*color_footer_link, new Color, new Color );
    var string footer_link_active_colors = generate_color_css( $*color_footer_link_active, new Color, new Color );
    var string footer_link_hover_colors = generate_color_css( $*color_footer_link_hover, new Color, new Color );
    var string footer_link_visited_colors = generate_color_css( $*color_footer_link_visited, new Color, new Color );

    var string entry_background = generate_background_css ($*image_background_entry_url, $*image_background_entry_repeat, $*image_background_entry_position, $*color_entry_background);
    var string module_background = generate_background_css ($*image_background_module_url, $*image_background_module_repeat, $*image_background_module_position, $*color_module_background);

    var string page_colors = generate_color_css($*color_page_text, $*color_page_background, $*color_page_border);
    var string entry_colors = generate_color_css($*color_entry_text, $*color_entry_background, $*color_entry_border);
    var string module_colors = generate_color_css($*color_module_text, $*color_module_background, $*color_module_border);

    var string page_title_colors = generate_color_css($*color_page_title, new Color, new Color);
    var string entry_title_colors = generate_color_css($*color_entry_title, $*color_entry_title_background, new Color);
    var string comment_title_colors = generate_color_css($*color_comment_title, $*color_comment_title_background, new Color);
    var string module_title_colors = generate_color_css($*color_module_title, $*color_module_title_background, new Color);

    var string page_link_colors = generate_color_css($*color_page_link, new Color, new Color);
    var string page_link_active_colors = generate_color_css($*color_page_link_active, new Color, new Color);
    var string page_link_hover_colors = generate_color_css($*color_page_link_hover, new Color, new Color);
    var string page_link_visited_colors = generate_color_css($*color_page_link_visited, new Color, new Color);

    var string entry_link_colors = generate_color_css($*color_entry_link, new Color, new Color);
    var string entry_link_active_colors = generate_color_css($*color_entry_link_active, new Color, new Color);
    var string entry_link_hover_colors = generate_color_css($*color_entry_link_hover, new Color, new Color);
    var string entry_link_visited_colors = generate_color_css($*color_entry_link_visited, new Color, new Color);
    var string entry_interaction_link_colors = generate_color_css($*color_entry_interaction_links, new Color, new Color);

    var string module_link_colors = generate_color_css($*color_module_link, new Color, new Color);
    var string module_link_active_colors = generate_color_css($*color_module_link_active, new Color, new Color);
    var string module_link_hover_colors = generate_color_css($*color_module_link_hover, new Color, new Color);
    var string module_link_visited_colors = generate_color_css($*color_module_link_visited, new Color, new Color);

    var string page_font = generate_font_css("", $*font_base, $*font_fallback, $*font_base_size, $*font_base_units);
    var string page_title_font = generate_font_css($*font_journal_title, $*font_base, $*font_fallback, $*font_journal_title_size, $*font_journal_title_units);
    var string page_subtitle_font = generate_font_css($*font_journal_subtitle, $*font_base, $*font_fallback, $*font_journal_subtitle_size, $*font_journal_subtitle_units);
    var string entry_title_font = generate_font_css($*font_entry_title, $*font_base, $*font_fallback, $*font_entry_title_size, $*font_entry_title_units);
    var string comment_title_font = generate_font_css($*font_comment_title, $*font_base, $*font_fallback, $*font_comment_title_size, $*font_comment_title_units);
    var string module_font = generate_font_css($*font_module_text, $*font_base, $*font_fallback, $*font_module_text_size, $*font_module_text_units);
    var string module_title_font = generate_font_css($*font_module_heading, $*font_base, $*font_fallback, $*font_module_heading_size, $*font_module_heading_units);

     var string userpic_css = "";
         if ($*userpics_position == "right") {
             $userpic_css = """
             .entry .userpic,
             .comment .userpic {
                float: right;
                margin: .5em 0 .5em .5em;
            }
             """;
        }
         else {
             $userpic_css = """
             .entry .userpic,
             .comment .userpic {
                 float: left;
                margin: .5em .5em .5em 0;
                 }
             """;
        } 

    var string navstrip_css = "";
        if ($*control_strip_reduced == true) {
            $navstrip_css = """
                td#lj_controlstrip_actionlinks,
                td#lj_controlstrip_search { display: none; }
            """;
        }
        else {
            $navstrip_css = """
                #lj_controlstrip td { float: left; }
            """;
        }

    """
    /* Page properties */
    body {
        $page_background
        $page_colors
        $page_font
        padding: 0;
        text-align: center; /* needed for IE */
        word-wrap: break-word;
    }
    
    html body {
        margin-top: 0;
        padding-top: 0;        
    }

    #lj_controlstrip {
        margin-bottom: 1em;  
        position: relative;
    }

    $navstrip_css

    blockquote { 
        border-left: 2px solid;
        padding: 0 1em; 
    }
    
    /* Page element positioning */
    #canvas {
        margin: 0 auto; 
        max-width: $*content_width;
        text-align: left; 
    }

    /* Header and footer */
    #header, 
    #footer {
        border-color: $*color_header_footer_border;
        border-style: solid;
        border-width: .5em .25em;
        clear: both;
        margin: 0;
        padding: .5em;
    }

    #header {
        $header_background
        $page_title_colors
        text-align: center;
    }

    #header h2 { 
        display: inline; 
    }

    #module-jump-link {
        text-align: right;
    }

    #module-jump-link a { color: $*color_header_link; }
    #module-jump-link a:visited { color: $*color_header_link_visited; }
    #module-jump-link a:hover { color: $*color_header_link_hover; }
    #module-jump-link a:active { color: $*color_header_link_active; }

    @media $*desktop_media_query {
        .multiple-columns #module-jump-link { display: none; }
    }


    /* Module positioning */
    #primary > .inner:first-child {
        padding: 0;
    }

    .one-column-split #secondary > .inner:first-child,
    .one-column-split #tertiary > .inner:first-child {
        padding: 0; 
    }

    .navigation,
    .talkform {
        $entry_colors
        border-style: solid;
        border-width: .5em .25em;
        margin: 1em 0; 
        padding: .5em;
    } /*yes, reply form is out of order, this is easier*/

.navigation a,
.talkform a {
    color: $*color_entry_link;
}

.navigation a:visited,
.talkform a:visited {
    color: $*color_entry_link_visited;
}

.navigation a:hover,
.talkform a:hover {
    color: $*color_entry_link_hover;
}

.navigation a:active,
.talkform a:active {
    color: $*color_entry_link_active;
}

    .navigation ul {
        margin: 0;
        padding-left: 0;
    }

    /* entries */
    .entry,
    .comment {
        position: relative;
    }

    .topnav,
    .bottomnav { 
       margin: 1em 0; 
       text-align: center;        
    }

    $userpic_css

    .entry {
         border-width: .5em .25em;
         margin: 1em 0;
         overflow: auto;
         padding: 1em;    
    }

    .entry .header {
         margin-bottom: 1em;
         margin-right: 50px; /*function this to match alignment*/
         min-height: 50px;
    }

    h3.entry-title { 
        margin: 0; 
    }

    .entry-content img {
        max-height: 100%; 
        max-width: 100%;  
    }

    .bottom-metadata { 
         margin-top: 1em; 
    }

    .top-metadata { 
         margin-bottom: 1em; 
    }
    .entry .footer { 
        text-align: right; 
    }

    .entry .tag { 
        margin: 1em 0; 
        text-align: left;
    }

    .tag ul {
        display: inline;
        margin-left: .5em;
        padding-left: 0;
    }
    
    .tag ul li {
        display: inline;
        padding: .25em;
    } /* same for month view */

    ul.entry-management-links {
        display: inline;
        margin-left: 0;
        padding-left: 0;
    }

    ul.entry-management-links li {
        display: inline;
        padding: 0 .5em;
    }

    ul.entry-interaction-links {
        display: inline; 
        margin-left: 0;
        padding-left: 0;
    }

    ul.entry-interaction-links li {
        display: inline;
        padding: 0 .5em;
    }

    /* comments */
    .talkform { 
        width: 100%; 
    }

    .comment-wrapper {
        padding: .5em 0;
        min-width: 15em;
    }

    .toppages,
    .bottompages { 
        margin: 1em 0; 
        text-align: center;
     }

    .bottomcomment { 
        margin: 1em 0; 
        text-align: right;
    }

    .comment,
    .toppages,
    .bottompages,
    .bottomcomment {
        $entry_colors
        border-style: solid;
        border-width: .5em .25em;
        margin-top: 0;
        padding: .5em;
    }

.comment a,
.toppages a,
.bottompages a,
.bottomcomment a {
    color: $*color_entry_link;
}

.comment a:visited,
.toppages a:visited,
.bottompages a:visited,
.bottomcomment a:visited {
    color: $*color_entry_link_visited;
}

.comment a:hover,
.toppages a:hover,
.bottompages a:hover,
.bottomcomment a:hover {
    color: $*color_entry_link_hover;
}

.comment a:active,
.toppages a:active,
.bottompages a:active,
.bottomcomment a:active {
    color: $*color_entry_link_active;
}

    .comment .comment-title {
        $comment_title_font
        margin: 0;
    }

    .comment .comment-title, 
    .comment .comment-title a {
        $comment_title_colors
    }

    .no-subject .comment .comment-title {
        background: none;
        border: none;
    }

    .comment-content { 
        margin: 1em 0; 
    }

    .comment .footer { 
        text-align: right; 
    }
    
    ul.comment-management-links {
        display: inline;
        margin-left: 0;
        padding-left: 0;
    }
  
    ul.comment-management-links li {
        display: inline;
        padding: 0 .5em;
    }

    ul.comment-interaction-links {
        display: inline;
        margin-left: 0;
        padding-left: 0;    
    }

    ul.comment-interaction-links li {
        display: inline;
        padding: 0 .5em;
    }

    .bottomcomment { 
        margin-top: 1em; 
    }

    .bottomcomment label { 
        display: block; 
    }

    /* Archive */

    table.month td,
    table.month th {
       border: 1px solid;
       padding: .25em .5em;
    }

    .month {
        $entry_colors
        border-style: solid;
        border-width: .5em .25em;
        margin: 1em 0; 
        padding: .5em;
    }

    /* Icons */

.icons-container {
    $entry_colors
    border-width: .5em .25em;
    margin: 1em 0;
    overflow: auto;
    padding: 1em;
}

.sorting-options ul {
    padding-left: 0;
    text-align: center;
}

.icons-container .icon {
    margin: 1em 0;
}

.icon-image {
    float: left;
    clear: left;
    margin-bottom: .25em;
    min-width: 100px;
    padding-right: 1em;
}

.icon-info {
    min-height: 100px;
}

.icon-info span {
    font-weight: bold;
}

.icon-info .default {
    text-decoration: underline;
}

.icon-keywords ul li {
    margin-left: 0;
    padding: .25em 0;
}

/* Tags */

.tags-container {
    $entry_colors
    border-width: .5em .25em;
    margin: 1em 0;
    overflow: auto;
    padding: 1em;
}

/* Link Colors for Archive, Icons and Tags */

.month a, .tags-container a, .icons-container a { color: $*color_entry_link; }
.month a:visited, .tags-container a:visited, .icons-container a:visited { color: $*color_entry_link_visited; }
.month a:hover, .tags-container a:hover, .icons-container a:hover { color: $*color_entry_link_hover; }
.month a:active, .tags-container a:active, .icons-container a:active { color: $*color_entry_link_active; }

    /* Modules */

    .module {
        border-width: .5em .25em;
        clear: both;
        margin: 1em 0;
        padding: .5em;
    }

    .one-column-split .module-navlinks li,
    .one-column-split .module-cuttagcontrols .module-header,
    .one-column-split .module-cuttagcontrols .module-content {
        display: inline;
    }

    .one-column-split .module-navlinks ul { 
        margin: .5em 0;
        text-align: center; 
    }
    
    .module-navlinks ul {
        list-style: none;
        margin-left: 0;
        padding-left: 0;    
    }

    .module-navlinks li {
        padding-right: 1.5em;
    }

    .one-column-split .module-cuttagcontrols h2.module-header {
        font-size: medium;
        margin: 0;
        padding: 0 1em 0 0;    
    }

    .cutTagControls img {
        margin: .25em 1em;
    }

    .module-calendar td {
        padding: 3px;
    }

    .module-calendar .entry-day { 
        background-color: $*color_module_highlight; 
    }

    .module-credit dt, .module-credit dd {
        display: inline;
        margin: 0;
        padding: 0;
    }

    .module-credit dt {
        font-weight: bold;
        margin-right: 0.5em;
    }

    .module-credit dd { 
        margin-right: .5em; 
    }

    """;
}
